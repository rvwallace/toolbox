# AI-related zsh widgets, keybindings, and aliases

# -----------------
# AI Widgets
# -----------------
# WARNING: AI-generated commands are inserted but NOT executed automatically.
# Review all commands before pressing Enter. AI may hallucinate dangerous commands.
# These widgets are conveniences, not guarantees of correctness.
# Always verify AI output before execution, especially for destructive operations.

# aichat
# Bind: Alt-e
_aichat_zsh() {
    if [[ -n "$BUFFER" ]]; then
        local _old=$BUFFER
        BUFFER+="⌛"
        zle -I && zle redisplay
        BUFFER=$(aichat -e "$_old")
        zle end-of-line
    fi
}
zle -N _aichat_zsh
bindkey -M emacs '^[e' _aichat_zsh
bindkey -M viins '^[e' _aichat_zsh

if (( ${+commands[aichat]} )); then
    autoload -U is-at-least

    _toolbox_aichat() {
        typeset -A opt_args
        typeset -a _arguments_options
        local ret=1

        if is-at-least 5.2; then
            _arguments_options=(-s -S -C)
        else
            _arguments_options=(-s -C)
        fi

        local context curcontext="$curcontext" state line
        local common=(
            '-m[Select a LLM model]:MODEL:->models'
            '--model[Select a LLM model]:MODEL:->models'
            '--prompt[Use the system prompt]:PROMPT: '
            '-r[Select a role]:ROLE:->roles'
            '--role[Select a role]:ROLE:->roles'
            '-s[Start or join a session]:SESSION:->sessions'
            '--session[Start or join a session]:SESSION:->sessions'
            '--empty-session[Ensure the session is empty]'
            '--save-session[Ensure the new conversation is saved to the session]'
            '-a[Start a agent]:AGENT:->agents'
            '--agent[Start a agent]:AGENT:->agents'
            '--agent-variable[Set agent variables]'
            '--rag[Start a RAG]:RAG:->rags'
            '--rebuild-rag[Rebuild the RAG to sync document changes]'
            '--macro[Execute a macro]:MACRO:->macros'
            '--serve[Serve the LLM API and WebAPP]'
            '-e[Execute commands in natural language]'
            '--execute[Execute commands in natural language]'
            '-c[Output code only]'
            '--code[Output code only]'
            '*-f[Include files, directories, or URLs]:FILE:_files'
            '*--file[Include files, directories, or URLs]:FILE:_files'
            '-S[Turn off stream mode]'
            '--no-stream[Turn off stream mode]'
            '--dry-run[Display the message without sending it]'
            '--info[Display information]'
            '--sync-models[Sync models updates]'
            '--list-models[List all available chat models]'
            '--list-roles[List all roles]'
            '--list-sessions[List all sessions]'
            '--list-agents[List all agents]'
            '--list-rags[List all RAGs]'
            '--list-macros[List all macros]'
            '-h[Print help]'
            '--help[Print help]'
            '-V[Print version]'
            '--version[Print version]'
            '*::text -- Input text:'
        )

        _arguments "${_arguments_options[@]}" $common && ret=0
        case $state in
            models|roles|sessions|agents|rags|macros)
                local -a values expl
                values=( ${(f)"$(_call_program values aichat --list-$state)"} )
                _wanted values expl $state compadd -a values && ret=0
                ;;
        esac
        return ret
    }

    compdef _toolbox_aichat aichat
fi
